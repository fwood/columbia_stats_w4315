# Redundant mean parameters for non-nested model: flight simulator example
# (same as in file M4.bug)

model {
  for (i in 1:n){
    y[i] ~ dnorm (y.hat[i], tau.y)
    y.hat[i] <- mu + g[treatment[i]] + d[airport[i]]
  }
  mu.adj <- mu + mean(g[]) + mean(d[])
  mu ~ dnorm (0, .0001)
  tau.y <- pow(sigma.y, -2)
  sigma.y ~ dunif (0, 100)

  for (j in 1:n.treatment){
    g[j] ~ dnorm (mu.g, tau.g)
    g.adj[j] <- g[j] - mean(g[])
  }
  mu.g ~ dnorm (0, .0001)
  tau.g <- pow(sigma.g, -2)
  sigma.g ~ dunif (0, 100)

  for (k in 1:n.airport){
    d[k] ~ dnorm (mu.d, tau.d)
    d.adj[k] <- d[k] - mean(d[])
  }
  mu.d ~ dnorm (0, .0001)
  tau.d <- pow(sigma.d, -2)
  sigma.d ~ dunif (0, 100)
}



